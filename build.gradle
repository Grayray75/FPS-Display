plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id 'io.github.juuxel.loom-vineflower' version "1.11.0"
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version_supported}"
group = project.maven_group

repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    includeFabricApiModule "fabric-api-base"
    includeFabricApiModule "fabric-key-binding-api-v1"
    includeFabricApiModule "fabric-lifecycle-events-v1"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    modApi("me.shedaniel.cloth:config-2:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

def includeFabricApiModule(String moduleName) {
    Object dependency = fabricApi.module(moduleName, project.fabric_version)
    dependencies.modImplementation(dependency)
    dependencies.include(dependency)
}

processResources {
    Map modJsonProperties = Map.of(
            "version", project.version,
            "mcversion", project.minecraft_version_supported
    )

    inputs.properties modJsonProperties

    filesMatching("fabric.mod.json") {
        expand modJsonProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    it.options.release = 8
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // mavenLocal()
    }
}

def ENV = System.getenv()

modrinth {
    token = ENV.MODRINTH_TOKEN

    projectId = "DIlqwRFH"
    versionNumber = "${project.mod_version}"
    versionName = "v${project.mod_version} (${project.minecraft_version_supported})"
    versionType = "release"
    changelog = "A changelog can be found at https://github.com/Grayray75/FPS-Display/blob/main/CHANGELOG.md"

    uploadFile = remapJar
    gameVersions = ["1.15", "1.15.1", "1.15.2"]
    loaders = ["fabric"]
    dependencies {
        optional.project "modmenu"
        optional.project "cloth-config"
    }
}

curseforge {
    if (ENV.CURSEFORGE_TOKEN) {
        apiKey = ENV.CURSEFORGE_TOKEN
    }

    project {
        id = "440228"
        releaseType = "release"
        changelog = "A changelog can be found at https://github.com/Grayray75/FPS-Display/blob/main/CHANGELOG.md"

        addGameVersion "1.15"
        addGameVersion "1.15.1"
        addGameVersion "1.15.2"
        addGameVersion "Fabric"
        addGameVersion "Java 8"

        mainArtifact(remapJar) {
            displayName = "v${project.mod_version} (${project.minecraft_version_supported})"
            relations {
                optionalDependency "modmenu"
                optionalDependency "cloth-config"
            }
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}
