plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id 'io.github.juuxel.loom-vineflower' version "1.11.0"
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version_supported}"
group = project.maven_group

repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    includeFabricApiModule "fabric-api-base"
    includeFabricApiModule "fabric-key-binding-api-v1"
    includeFabricApiModule "fabric-lifecycle-events-v1"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

def includeFabricApiModule(String moduleName) {
    Object dependency = fabricApi.module(moduleName, project.fabric_version)
    dependencies.modImplementation(dependency)
    dependencies.include(dependency)
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // mavenLocal()
    }
}

def ENV = System.getenv()

modrinth {
    token = ENV.MODRINTH_TOKEN

    projectId = "DIlqwRFH"
    versionNumber = "${project.mod_version}"
    versionName = "[${project.minecraft_version_supported}] FPS-Display v${project.mod_version}"
    versionType = "release"
    changelog = "A changelog can be found at https://github.com/Grayray75/FPS-Display/blob/v3/CHANGELOG.md"
    
    uploadFile = remapJar
    gameVersions = ["1.20", "1.20.1"]
    loaders = ["fabric"]
    dependencies {
        optional.project "modmenu"
        optional.project "cloth-config"
    }
}

curseforge {
    if (ENV.CURSEFORGE_TOKEN) {
        apiKey = ENV.CURSEFORGE_TOKEN
    }

    project {
        id = "440228"
        releaseType = "release"
        changelog = "A changelog can be found at https://github.com/Grayray75/FPS-Display/blob/v3/CHANGELOG.md"
        
        addGameVersion "1.20"
        addGameVersion "1.20.1"
        addGameVersion "Fabric"
        addGameVersion "Java 17"

        mainArtifact(remapJar) {
            displayName = "[${project.minecraft_version_supported}] FPS-Display v${project.mod_version}"
            relations {
                optionalDependency "modmenu"
                optionalDependency "cloth-config"
            }
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}
