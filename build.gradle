plugins {
    id "fabric-loom" version "1.14-SNAPSHOT"
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
    id "maven-publish"
}

base.archivesName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://maven.isxander.dev/releases/" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    includeFabricApiModule "fabric-api-base"
    includeFabricApiModule "fabric-key-binding-api-v1"
    includeFabricApiModule "fabric-lifecycle-events-v1"

    // Provides the latest farbic-api in dev runtime for mod dependencies
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modApi("dev.isxander:yet-another-config-lib:${project.yacl_config_version}-fabric")
}

def includeFabricApiModule(String moduleName) {
    Object dependency = fabricApi.module(moduleName, project.fabric_version)
    dependencies.modImplementation(dependency)
    dependencies.include(dependency)
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    // Minecraft 1.20.5 (24w14a) upwards uses Java 21.
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
        // mavenLocal()
    }
}

publishMods {
    file = remapJar.archiveFile
    type = STABLE
    displayName = "v${project.mod_version} (${project.minecraft_version})"
    changelog = "A changelog can be found at https://github.com/Grayray75/FPS-Display/blob/main/CHANGELOG.md"

    modLoaders.add("fabric")
    modLoaders.add("quilt")

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "DIlqwRFH"

        minecraftVersions.add("1.21.10")
        minecraftVersions.add("1.21.9")

        optional("modmenu")
        optional("cloth-config")
        optional("yacl")
    }

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "440228"

        clientRequired = true
        javaVersions.add(JavaVersion.VERSION_21)
        minecraftVersions.add("1.21.10")
        minecraftVersions.add("1.21.9")

        optional("modmenu")
        optional("cloth-config")
        optional("yacl")
    }
}
