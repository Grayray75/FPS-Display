plugins {
    id "fabric-loom" version "1.4-SNAPSHOT"
    id 'legacy-looming' version "1.4-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+"
    id "maven-publish"
}

base.archivesName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version_supported}"
group = project.maven_group

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings legacy.yarn(project.minecraft_version, project.yarn_mappings_build)
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    Map modJsonProperties = Map.of(
            "version", project.version,
            "mcversion", project.minecraft_version_supported
    )

    inputs.properties modJsonProperties

    filesMatching("fabric.mod.json") {
        expand modJsonProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // mavenLocal()
    }
}

def ENV = System.getenv()

modrinth {
    token = ENV.MODRINTH_TOKEN

    projectId = "DIlqwRFH"
    versionNumber = "${project.mod_version}"
    versionName = "v${project.mod_version} (${project.minecraft_version_supported})"
    versionType = "release"
    changelog = "A changelog can be found at https://github.com/Grayray75/FPS-Display/blob/main/CHANGELOG.md"

    uploadFile = remapJar
    gameVersions = ["1.13.2"]
    loaders = ["fabric"]
    dependencies {
        optional.project "legacy-mod-menu"
    }
}
